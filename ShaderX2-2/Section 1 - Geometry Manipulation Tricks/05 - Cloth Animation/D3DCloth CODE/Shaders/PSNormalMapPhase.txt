ps_3_0

; Samplers
dcl_2d    s0    ;MRT0 (X ,Y ) 
dcl_2d    s1    ;MRT1 (Z ,Nx)

; Inputs
dcl_texcoord0	v0.xy    ; Main Node Sample Coord
dcl_texcoord1	v1.xy    ; Right Node Sample Coord
dcl_texcoord2	v2.xy    ; Top Node Sample Coord
dcl_texcoord3	v3.xy    ; Left Node Sample Coord
dcl_texcoord4	v4.xy    ; Bottom Node Sample Coord

def c19, 0.0, 0.0, 0.0, 0.0

; Main sample = avg of sample pos 0 and 5

texld r0 , v0, s0        ;Center Node (X,Y)
texld r11, v0, s1        ;Center Node (Z)
mov oC0, r0	         ;Output (X,Y) to MRT0

texld r0, v1, s0         ;Right Node (X,Y)
texld r1, v1, s1         ;Right Node (Z)
mov   r3.xy, r0
mov   r3.z,  r1.x        ;Right Node (X,Y,Z)

texld r0, v2, s0         ;Top Node (X,Y) 
texld r1, v2, s1         ;Top Node (Z)
mov   r4.xy, r0
mov   r4.z,  r1.x        ;Top Node (X,Y,Z)

texld r9 , v3, s0        ;Left Node (X,Y)
texld r10, v3, s1        ;Left Node (Z)
mov   r5.xy, r9
mov   r5.z,  r10.x       ;Left Node (X,Y,Z)

texld r9 , v4, s0        ;Bottom Node (X,Y)
texld r10, v4, s1        ;Bottom Node (Z)
mov   r6.xy, r9
mov   r6.z,  r10.x       ;Bottom Node (X,Y,Z)

; create vectors for cross product

add r0.xyz, r3.xyz, -r5.xyz
add r1.xyz, r4.xyz, -r6.xyz

; cross product and normalisation

crs r7.xyz, r0, r1
nrm r0, r7.xyz            ;Vertex Normal


; Output results to MRT1 and MRT2
mov r11.y, r0.x
mov r9  , r0.y
mov r9.g, r0.z

mov oC1, r11	; (Z, Nx)
mov oC2, r9	; (Ny,Nz)